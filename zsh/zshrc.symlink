# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/prog

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zshpiece)

# load the path files
for file in ${(M)config_files:#*/path.zshpiece}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zshpiece}:#*/completion.zshpiece}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zshpiece}
do
  source $file
done

unset config_files
